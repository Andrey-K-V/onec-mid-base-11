#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	// ++ Купцов Задача ДК_Доработка 13/09/24
	
	ИмяФормы = Форма.имяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппуШапкаПраво(Форма);
		//ДобавитьГруппаСкидкаВГруппуШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппуШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппуШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);	
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
	// --
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицоВГруппуШапкаПраво(Форма)
	// ++ Купцов Задача ДК_Доработка 13/09/24
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.путьКДанным = "Объект.ДК_КонтактноеЛицо";
	// --
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	// ++ Купцов Задача ДК_Доработка 13/09/24
	ПолеВвода = Форма.Элементы.Вставить("ДК_КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.путьКДанным = "Объект.ДК_КонтактноеЛицо";
	// --
КонецПроцедуры

Процедура ДобавитьГруппаСкидкаВГруппуШапкаЛево(Форма)
	
	// ++ Купцов Задача ДК_Доработка 13/09/24
	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидка.ОтображатьЗаголовок = Ложь;
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), ГруппаСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.путьКДанным = "Объект.ДК_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ДК_СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчитатьТаблицу = Форма.Команды.Добавить("ПересчитатьТаблицу");
	КомандаПересчитатьТаблицу.Заголовок = "Пересчитать таблицу";
	КомандаПересчитатьТаблицу.Действие = "ДК_ПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"),ГруппаСкидка);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
	// --
КонецПроцедуры

#КонецОбласти